apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    def vPropsFile = file('version.properties')
    if (vPropsFile.canRead()) {
        def vProps = new Properties()
        vProps.load(new FileInputStream(vPropsFile))

        def value = 0
        def runTasks = gradle.startParameter.taskNames
        if ('assemble' in runTasks || 'assembleRelease' in runTasks || 'aR' in runTasks) {
            value = 1;
        }

        def vMajor  = 0
        def vMinor  = 0
        def vPatch  = vProps['VERSION_PATCH'].toInteger() + value
        def vBuild  = vProps['VERSION_BUILD'].toInteger() + 1
        def vNumber = vProps['VERSION_NUMBER'].toInteger() + value

        vProps['VERSION_PATCH'] = vPatch.toString()
        vProps['VERSION_BUILD'] = vBuild.toString()
        vProps['VERSION_NUMBER'] = vNumber.toString()
        vProps.store(vPropsFile.newWriter(), null)

        defaultConfig {
            applicationId "com.onsoim.ledger"
            minSdkVersion 27
            targetSdkVersion 29
            versionCode vNumber
            versionName "${vMajor}.${vMinor}.${vPatch}.${vBuild}"

            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }
    } else {
        throw new GradleException("Could not read version.properties!")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

}